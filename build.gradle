buildscript {
	repositories {
		jcenter()
	}
	dependencies {
//		classpath 'pl.allegro.tech.build:axion-release-plugin:1.10.0'
		classpath 'io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
	}
}

//apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'eclipse'

ext {
	snapshotVersion = "0.${new Date().format('yyyyMMdd')}"
}

//scmVersion {
//	useHighestVersion = true
//}

group = 'com.ascelion.rest-bridge'
//version = scmVersion.version
version = '1.2.0.1-SNAPSHOT'

task currentVersion {
	doLast {
		println version
	}
}

tasks.create( name: 'test-report', type: TestReport ) { rep ->
	destinationDir = file("$buildDir/reports/allTests")

	subprojects { sub ->
		sub.tasks.withType( Test ) { tst ->
			reports.html.enabled = false

			rep.reportOn tst
		}
	}
}

allprojects {
	repositories {
		jcenter()
		maven {
			url = 'https://repo.gradle.org/gradle/libs-releases'
		}
		maven {
			url = 'https://repository.apache.org/content/repositories/snapshots'
			content {
				includeGroup 'org.apache.cxf'
			}
		}
	}

	defaultTasks 'test'
}

subprojects { sub ->
	ext {
		mpLocal  = rootProject.file( 'mp-local/eclipse/pom.xml' ).exists()
		mpApiDir = rootProject.file 'mp-local/eclipse/api/src/main/java'
		mpTckDir = rootProject.file 'mp-local/eclipse/tck/src/main/java'
	}

	apply plugin: ascelion.rest.bridge.BuildPlugin
	apply from: rootProject.file( 'dependencies.gradle' )

	group = rootProject.group
	version = rootProject.version

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	sourceSets.all { src ->
		src.compileClasspath += src.annotationProcessorPath
	}

	tasks.withType( Test ) {
		ignoreFailures = booleanProperty 'ignoreFailures'
		enableAssertions = true
		scanForTestClasses = true
		forkEvery = 1

		testLogging {
			exceptionFormat 'full'
			showStandardStreams = true
		}

		debug = booleanProperty( 'arq.debug' )
//		systemProperty "force", UUID.randomUUID()
	}

	plugins.withType( MavenPublishPlugin ) {
		sub.apply from: rootProject.file( 'publish.gradle' )
	}
}
