buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath group: 'pl.allegro.tech.build', name: 'axion-release-plugin', version: '1.10.0'
	}
}

apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'eclipse'

task publish {
	doLast {
		println 'no publishing defined yet'
	}
}

allprojects {
	repositories {
		mavenLocal()
		mavenCentral()
		maven {
			url = 'https://repo.gradle.org/gradle/libs-releases'
		}
		maven {
			url = 'https://repository.apache.org/content/repositories/snapshots'
			content {
				includeGroup 'org.apache.cxf'
			}
		}
	}

	defaultTasks 'test'
}

subprojects { sub ->
	apply plugin: ascelion.rest.bridge.BuildPlugin
	apply from: rootProject.file( 'dependencies.gradle' )

	group = 'ascelion.rest-bridge'
	version = '1.0.2-SNAPSHOT'

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	sourceSets.all { src ->
		src.compileClasspath += src.annotationProcessorPath
	}

	tasks.withType( Test ) {
		group = 'verification'

		if( name == 'arquillian' ) {
			description = 'Runs all Arquillian checks.'
		}

		ignoreFailures = true
		enableAssertions = true
		scanForTestClasses = true

		testLogging {
			exceptionFormat 'full'
			showStandardStreams = true
		}
		reports {
			html.enabled = true
		}

		systemProperty "seed", UUID.randomUUID()
	}

	tasks.withType( War ) {
		jar.enabled = true

		apply plugin: 'eclipse-wtp'

		eclipse {
			wtp {
				facet {
					facet name: 'jst.web', version: '3.1'

					file {
						whenMerged { facets ->
							facets.facets.removeIf { it.name == 'jst.utility' }
						}
					}
				}
			}
		}
	}
	
	plugins.withType( MavenPublishPlugin ) {
		sub.apply from: rootProject.file( 'publish.gradle' )
	}
}
