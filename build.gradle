allprojects {
	repositories {
		jcenter()
		maven {
			url = 'https://repo.gradle.org/gradle/libs-releases'
		}
	}

	defaultTasks 'build'
}

apply plugin: 'eclipse'

subprojects {
	apply plugin: ascelion.rest.bridge.BuildPlugin
	apply from: rootProject.file( 'dependencies.gradle' )

	group = 'ascelion.rest-bridge'
	version = '1.0.2-SNAPSHOT'

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	tasks.withType( Test ) {
		outputs.upToDateWhen { false }

		ignoreFailures = true
		enableAssertions = true
		scanForTestClasses = true

		testLogging {
			exceptionFormat 'full'
			showStandardStreams = true
		}
		reports {
			html.enabled = true
		}
	}

	tasks.withType( War ) {
		jar.enabled = true

		apply plugin: 'eclipse-wtp'

		eclipse {
			wtp {
				facet {
					facet name: 'jst.web', version: '3.1'

					file {
						whenMerged { facets ->
							facets.facets.removeIf { it.name == 'jst.utility' }
						}
					}
				}
			}
		}
	}

	plugins.withType( ascelion.rest.bridge.ASPlugin ) {
		containers {
			payara41( payara ) {
				distribution 'fish.payara.distributions:payara:4.1.2.+@zip'
			}
			payara5( payara ) {
				distribution 'fish.payara.distributions:payara:5.+@zip'
			}
			gf4( glassfish ) {
				distribution 'org.glassfish.main.distributions:glassfish:4.+@zip'
			}
			gf5( glassfish ) {
				distribution 'org.glassfish.main.distributions:glassfish:5.+@zip'
			}
		}
	}
}
