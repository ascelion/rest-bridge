apply plugin: 'io.spring.dependency-management'

def versions = [
	slf4j: '1.7.25',
	asm: '7.0',
	micro: [
		rest: '1.2-m2',
		config: '1.2',
	]
]

configurations {
	externalApi

	compileOnly.extendsFrom externalApi
	testCompileOnly.extendsFrom externalApi
	testRuntimeOnly.extendsFrom externalApi
}

dependencyManagement {
	imports {
		mavenBom 'org.apache.cxf:cxf-parent:3.3.0-SNAPSHOT'
		mavenBom 'org.glassfish.jersey:jersey-bom:2.27'
		mavenBom 'org.jboss.resteasy:resteasy-bom:3.6.2.Final'
		mavenBom 'org.jboss.arquillian:arquillian-bom:1.4.1.Final'
		mavenBom 'org.jboss.shrinkwrap:shrinkwrap-bom:1.2.6'
		mavenBom 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-bom:3.1.3'
		mavenBom 'org.jboss.weld:weld-core-bom:3.0.5.Final'
		mavenBom 'org.eclipse.jetty:jetty-bom:9.4.14.v20181114'
		mavenBom 'com.fasterxml.jackson:jackson-bom:2.9.7'
	}

	dependencies {
		dependency 'io.leangen.geantyref:geantyref:1.3.6'
		dependency 'javax.servlet:javax.servlet-api:4.0.1'
		dependency 'javax.validation:validation-api:2.0.1.Final'
		dependency 'javax.annotation:javax.annotation-api:1.3'
		dependency 'javax.ws.rs:javax.ws.rs-api:2.1.1'
		dependency 'javax.xml.bind:jaxb-api:2.3.1'
		dependency 'javax.inject:javax.inject:1'
		dependency 'javax.json:javax.json-api:1.1.4'
		dependency 'javax.json.bind:javax.json.bind-api:1.0'
		dependency 'javax.enterprise:cdi-api:2.0'
		dependency 'javax.el:javax.el-api:3.0.1-b06'

		dependency "org.eclipse.microprofile.rest.client:microprofile-rest-client-api:${versions.micro.rest}"
		dependency "org.eclipse.microprofile.rest.client:microprofile-rest-client-tck:${versions.micro.rest}"

		dependency "org.eclipse.microprofile.config:microprofile-config-api:${versions.micro.config}"
		dependency "org.apache.geronimo.config:geronimo-config-impl:${versions.micro.config}"

		dependency 'org.glassfish:javax.json:1.1.3'
		dependency 'org.glassfish:javax.el:3.0.1-b10'
		dependency 'io.leangen.geantyref:geantyref:1.3.6'
		dependency 'org.hibernate:hibernate-validator:6.0.13.Final'
		dependency 'org.eclipse:yasson:1.0.2'
		dependency "org.ow2.asm:asm:${versions.asm}"

		dependency 'org.apache.commons:commons-io:1.3.2'
		dependency 'org.apache.commons:commons-text:1.6'
		dependency 'org.apache.commons:commons-lang3:3.8.1'
		dependency 'commons-codec:commons-codec:1.11'
		dependency 'commons-logging:commons-logging:1.2'
		dependency 'org.apache.httpcomponents:httpclient:4.5.6'

		dependency 'org.javassist:javassist:3.24.1-GA'

		dependency 'org.jboss.arquillian.container:arquillian-jetty-embedded-9:1.0.0.CR3'
		dependency 'com.github.tomakehurst:wiremock:2.19.0'

		dependency "org.slf4j:slf4j-api:${versions.slf4j}"
		dependency 'ch.qos.logback:logback-classic:1.2.3'
		dependency 'org.jboss:jandex:2.0.5.Final'

		dependency 'org.mockito:mockito-core:2.23.4'
		dependency 'junit:junit:4.12'

		dependencySet( group: 'org.hamcrest', version: '2.1' ) {
			entry 'hamcrest-core'
			entry 'hamcrest-library'
		}

		dependency 'org.testng:testng:6.14.3'
		dependency 'com.beust:jcommander:1.72'
	}
}

configurations.all { cf ->
	if( cf.name.endsWith( 'nnotationProcessor' ) ) {
		dependencies {
			add cf.name, 'org.projectlombok:lombok:1.18.4'
		}
	}

	cf.exclude group: 'org.glassfish.hk2.external', module: 'javax.inject'
	cf.exclude group: 'org.jboss.spec.javax.ws.rs', module: 'jboss-jaxrs-api_2.1_spec'

	if( mpLocal ) {
		resolutionStrategy.dependencySubstitution {
			substitute module('org.eclipse.microprofile.rest.client:microprofile-rest-client-api') with project( ':mp-local:api' )
			substitute module('org.eclipse.microprofile.rest.client:microprofile-rest-client-tck') with project( ':mp-local:tck' )
		}
	}
}
