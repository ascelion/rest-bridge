def gext       = gradle.ext
def signKey    = "${rootProject.name}.sign.keyId"
def bintrayURL = "${rootProject.name}.bintray.url"
def bintrayUSR = "${rootProject.name}.bintray.username"
def bintrayPWD = "${rootProject.name}.bintray.password"

task sourcesJar(type: Jar) {
	from sourceSets.main.allJava
	classifier = 'sources'
}

task javadocJar(type: Jar) {
	from javadoc
	classifier = 'javadoc'
}

publishing {
	publications {
		maven( MavenPublication ) {
			from components.java

			artifact sourcesJar
			artifact javadocJar

			afterEvaluate {
				artifactId = "${rootProject.name}-${project.name}"
			}
		}
	}
}

if( gradle.hasProperty( signKey ) ) {
	apply plugin: 'signing'

	ext['signing.gnupg.keyName'] = gext[signKey]

	signing {
		useGpgCmd()
		sign publishing.publications.maven
	}
}
